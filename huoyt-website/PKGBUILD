# Maintainer: div72 <aur@div72.xyz>
pkgname="huoyt-website"
pkgver="1.0.2"
pkgrel=1
pkgdesc="The website for the free software club of Hacettepe University."
arch=("any")
url="https://github.com/hacettepeoyt/website"
license=('MIT')

depends=("nodejs" "mongodb")
makedepends=("nodejs" "npm")

backup=("etc/huoyt-website/config.sh")

source=("https://github.com/hacettepeoyt/website/archive/refs/tags/v${pkgver}.tar.gz"
        "huoyt-website"
        "huoyt-website.service"
        "huoyt-website.sysusers"
        "huoyt-website.tmpfiles")
sha256sums=("9bcab3a813629025d27d06b9e1ba8996ddb933ce226f8d67360e7c971e9cb104"
            "SKIP"
            "SKIP"
            "SKIP"
            "SKIP")

build() {
    cd "${srcdir}/website-${pkgver}"
    npm install
}

package() {
    mkdir -p "${pkgdir}/opt/"
    cp -r "${srcdir}/website-${pkgver}/" "${pkgdir}/opt/"
    mv "${pkgdir}/opt/website-${pkgver}" "${pkgdir}/opt/${pkgname}"
    chmod -R 755 "${pkgdir}/opt/${pkgname}/"
    install -Dm755 "${srcdir}/huoyt-website" "${pkgdir}/usr/bin/huoyt-website"
    mkdir -p "${pkgdir}/etc/huoyt-website"
    chmod 660 "${pkgdir}/etc/huoyt-website/"
    touch "${pkgdir}/etc/huoyt-website/config.sh"
    chmod 660 "${pkgdir}/etc/huoyt-website/config.sh"
    install -Dm644 "${srcdir}/huoyt-website.service" "${pkgdir}/usr/lib/systemd/system/huoyt-website.service"
    install -Dm644 "${srcdir}/huoyt-website.sysusers" "${pkgdir}/usr/lib/sysusers.d/huoyt-website.conf"
    install -Dm644 "${srcdir}/huoyt-website.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/huoyt-website.conf"

    chown -R root:root "${pkgdir}"
}
