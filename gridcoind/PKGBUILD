# Original PKGBUILD written by Tahvok.

pkgname=gridcoind
pkgver=5.3.3.0
pkgrel=0
pkgdesc="A cryptocurrency that rewards users for participating on the BOINC network"

arch=('x86_64')
url="https://gridcoin.us"
license=('MIT')

depends=("rtmpdump")

_sourcename="Gridcoin-Research-$pkgver"

source=("$_sourcename.tar.gz::https://github.com/gridcoin-community/Gridcoin-Research/archive/$pkgver.tar.gz"
	"gridcoin.sysusers"
	"gridcoin.tmpfiles"
	"gridcoind.service"
	"gridcoin.conf"
	"00_exit_after_forking.patch")

sha256sums=('45c80a24a289c5488df8cf3fdc713b19679a9a248de75227b552d72321ac0ed7'
	    'adca82b39386e0aac7ff2fadab491e7181b866ab6c98b2080b8d421e2e4276c0'
    	    'ecff0da53b120f202864df881c921e4eb1dbf8e5f030f98e1fc684a38d4e2f2b'
    	    '1bd2f6026428fe1626d1d19d67cb08364ace475048986f374fa9415c17edb611'
    	    '506b5a615944ce5cc75f4bed867ad5ca9d95a6fb5c9b4d641f03c499a4756106'
	    '7f99d7962066568f23682a0be29a35cae97a6bbe5cb1a040bd67fd6e0ce12700')

prepare() {
  cd "$srcdir/$_sourcename"

  patch src/init.cpp "$srcdir/00_exit_after_forking.patch"
}

build() {
  cd "$srcdir/$_sourcename"

  ./autogen.sh
  HOST=x86_64-pc-linux-gnu NO_QT=1 NO_UPNP=1 BDB_53=1 make -j8 -C depends
  ./configure --prefix=$(pwd)/depends/x86_64-pc-linux-gnu --with-incompatible-bdb --disable-tests --enable-reduce-exports
  make -j8
  strip src/gridcoinresearchd
}

package() {
  backup=('etc/gridcoin/gridcoin.conf')

  cd "$srcdir/$_sourcename"

  install -Dm755 src/gridcoinresearchd "$pkgdir/usr/bin/gridcoind"
  install -Dm644 doc/gridcoinresearchd.1 "$pkgdir/usr/share/man/man1/gridcoind.1"
  install -Dm644 "$srcdir/gridcoind.service" "$pkgdir/usr/lib/systemd/system/gridcoind.service"
  install -Dm644 "$srcdir/gridcoin.sysusers" "$pkgdir/usr/lib/sysusers.d/gridcoin.conf"
  install -Dm644 "$srcdir/gridcoin.tmpfiles" "$pkgdir/usr/lib/tmpfiles.d/gridcoin.conf"
  install -Dm644 "$srcdir/gridcoin.conf" "$pkgdir/etc/gridcoin/gridcoin.conf"

  install -Dm644 COPYING "$pkgdir/usr/share/licenses/$pkgname/COPYING"
}

